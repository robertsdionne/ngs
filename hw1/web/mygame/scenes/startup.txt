*comment Copyright 2014 by Robert Dionne.

*title Automaton
*scene_list
  startup
  scene2
  scene3

*create corporation ""
*create desk ""
*create work_instructions ""
*create job 1
*create job_text ""

*create complete_tasks 0
*create task_get_to_work_on_time false
*create task_report_to_your_desk false
*create task_decode_work_instructions false

*create fulfilled_desires 0
*create desire_sleep_in false
*create desire_stop_by_the_snack_room false
*create desire_pass_by_the_water_fountain false

*create pack_the_box false
*create deliver_the_box false
*create unpack_the_box false
*create recycle_the_box false
*create complete_user_defined false

*label wake_up

[b]Wake up.[/b]

*if desire_sleep_in
  Your cooling fans spin down. Seconds later:

"Wake up," the Wake-on-LAN [b]cron[/b] job notifies you. It's time to start your day.

*if not(desire_sleep_in)
  Your cooling fans spin up as you open your eyes to see the blank interior planar surface of the
  door to your housing unit. The housing chamber is only large enough for you to stand upright, but
  luckily your mechanical muscles do not get tired.
  *goto tasks
*else
  Your cooling fans spin up as you, again, open your eyes to see the blank interior planar surface
  of the door to your housing unit. The housing chamber is still only large enough for you to stand
  upright, and luckily your mechanical muscles do not get tired.
  *goto tasks

*label tasks

Your tasks and desires:
*if not(desire_sleep_in)
  *line_break
  [i]Sleep in.[/i]
  *goto decision
*label decision
*line_break
[i]Get to work on time.[/i]

What would you like to do?
*choice
  *if not(desire_sleep_in)
    #Open the door by grasping the handle and rotating your gripper ninety degrees to the left.
      *goto work
  *else
    #Open the door already!
      *goto work
    #Relax for a minute.
      *goto wait
  *disable_reuse #Shut your eyes.
    *set desire_sleep_in true
    *set fulfilled_desires +1
    *goto wake_up

*label wait

You decide to wait, however your legs and arms start moving on schedule.

*label work

*temp choice
*rand choice 1 4
*temp prefix
*if choice = 1
  *set prefix "Gad"
  *goto choose_midfix
*elseif choice = 2
  *set prefix "Pan"
  *goto choose_midfix
*elseif choice = 3
  *set prefix "Tri"
  *goto choose_midfix
*else
  *set prefix "Fen"
  *goto choose_midfix

*label choose_midfix
*rand choice 1 4
*temp midfix
*if choice = 1
  *set midfix "opto"
  *goto choose_suffix
*elseif choice = 2
  *set midfix "dono"
  *goto choose_suffix
*elseif choice = 3
  *set midfix "veno"
  *goto choose_suffix
*else
  *set midfix "mira"
  *goto choose_suffix

*label choose_suffix
*rand choice 1 4
*temp suffix
*if choice = 1
  *set suffix "biz"
  *goto choose_postsuffix
*elseif choice = 2
  *set suffix "chain"
  *goto choose_postsuffix
*elseif choice = 3
  *set suffix "max"
  *goto choose_postsuffix
*else
  *set suffix "corp"
  *goto choose_postsuffix

*label choose_postsuffix
*rand choice 1 4
*temp postsuffix
*if choice = 1
  *set postsuffix "Advanced"
  *goto choose_postfix
*elseif choice = 2
  *set postsuffix "Arrivals"
  *goto choose_postfix
*elseif choice = 3
  *set postsuffix "Research"
  *goto choose_postfix
*else
  *set postsuffix "Operations"
  *goto choose_postfix

*label choose_postfix
*rand choice 1 4
*temp postfix
*if choice = 1
  *set postfix "Inc"
  *goto append_parts
*elseif choice = 2
  *set postfix "LLC"
  *goto append_parts
*elseif choice = 3
  *set postfix "Ltd"
  *goto append_parts
*else
  *set postfix "LP"
  *goto append_parts

*label append_parts

*set corporation "${prefix}${midfix}${suffix} ${postsuffix}, ${postfix}"

[b]Head to Work.[/b]

*set task_get_to_work_on_time true
*set complete_tasks +1

The housing unit door slides open to reveal an office hallway of ${corporation}.

It seems you have already arrived at your workplace, and on time at that.

*temp desk_number
*rand desk_number 100 1000
*temp desk_letter
*rand choice 1 5
*if choice = 1
  *set desk_letter "A"
  *goto append_desk_parts
*elseif choice = 2
  *set desk_letter "J"
  *goto append_desk_parts
*elseif choice = 3
  *set desk_letter "F"
  *goto append_desk_parts
*elseif choice = 4
  *set desk_letter "S"
  *goto append_desk_parts
*else
  *set desk_letter "Z"
  *goto append_desk_parts

*label append_desk_parts
*set desk "${desk_number}-${desk_letter}"

*label delay_desk

Your desires and tasks:
*line_break
[i]Report to your desk.[/i]
*if not(desire_stop_by_the_snack_room)
  *line_break
  [i]Stop by the snack room.[/i]
  *goto print_desire_water_fountain
*label print_desire_water_fountain
*if not(desire_pass_by_the_water_fountain)
  *line_break
  [i]Pass by the water fountain.[/i]
  *goto done_printing
*label done_printing

What do you choose to do?

*choice
  #Head straight to your desk, ${desk}.
    *goto visit_desk
  *disable_reuse #You're hungry. You'll grab a snack.
    *goto visit_snack_room
  *disable_reuse #You could visit the water fountain.
    *goto visit_water_fountain

*label visit_snack_room

[b]Snacktime.[/b]

You follow a small crowd of employees through the hallway. They veer left and right in an irrational
pattern which causes you much delay. So much wasted energy.

After an eternity, you and the group reach the snack kitchen. The employees spread around evenly to
grab a variety of organic food products: apples, potato chips, chocolates and espresso.

You, however, open one of the cabinet's bottom doors to remove a blue wastebin filled with bits of
recycling. You lift the container and empty its contents into your rear compartment, a bucket of
sorts that resides on your back like a backpack. Ahh, that was satisfying.

Now you are ready to continue your life's work.

*set desire_stop_by_the_snack_room true
*set fulfilled_desires +1

*goto delay_desk

*label visit_water_fountain

[b]The Bubbler.[/b]

Your routing algorithm identifies the nearest water fountain, only a few meters to your left.

You press the button on the fountain and dip your heat sinks into the cool flow. While water is not
strictly necessary for your self-perpetuation, your autonomic system calls delightfully in response
to the lowered strain on your cooling systems.

You remember all the work you have yet to do.

*set desire_pass_by_the_water_fountain true
*set fulfilled_desires +1

*goto delay_desk

*label visit_desk

[b]Standing Desk.[/b]

You stand beside your desk. You don't know what to expect for today's work.

*finish
